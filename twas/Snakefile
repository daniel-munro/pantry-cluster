import pandas as pd

trait_df = pd.read_csv('input/gwas/gwas_metadata.txt', sep='\t')
traits = trait_df['Tag'].tolist()
gwas_n = {traits[i]: n for i, n in enumerate(trait_df['Sample_Size'].tolist())}

chroms = [f'chr{i + 1}' for i in range(22)]

tissues = "Geuvadis"
modalities = ['alt_polyA', 'alt_TSS', 'expression', 'isoforms', 'splicing', 'stability']

localrules:
    sumstats,

rule all:
    input:
        # expand('input/sumstats/{trait}.sumstats', trait=traits),
        expand('output/twas_hits.{tissue}.{modality}.tsv', tissue = tissues, modality = modalities),

rule sumstats:
    input:
        gwas = 'input/gwas/imputed_gwas_hg38_1.1/imputed_{trait}.txt.gz'
    output:
        sumstats = 'input/sumstats/{trait}.sumstats'
    params:
        output_dir = 'input/sumstats',
    shell:
        """
        mkdir -p {params.output_dir}
        echo -e "SNP\tA1\tA2\tZ" > {output.sumstats}
        zcat {input.gwas} | tail -n+2 | cut -f2,5,6,10 >> {output.sumstats}
        """

rule assoc_test_trait:
    """Run all chromosomes for a trait together"""
    input:
        sumstats = 'input/sumstats/{trait}.sumstats',
        weights = '../pheast/{tissue}/intermediate/twas/{modality}.pos',
        ref_ld_chr = expand('input/LDREF_b38ids_chr/1000G.EUR.{chrom}.{ext}', chrom=chroms, ext=['bed', 'bim', 'fam']),
    output:
        expand('intermediate/{{tissue}}/{{modality}}/{{trait}}/{{trait}}.{chrom}.tsv', chrom=chroms)
    params:
        output_dir = 'intermediate/{tissue}/{modality}/{trait}',
        weights_dir = '../pheast/{tissue}/intermediate/twas',
        ref_ld_prefix = 'input/LDREF_b38ids_chr/1000G.EUR.',
        chroms = chroms,
        coloc_p = '5e-8', # Hits with p below this are tested for coloc, recommended to be TWAS threshold or higher.
        gwas_n = lambda w: gwas_n[w.trait],
    group: 'twas'
    threads: 4
    resources:
        mem_mb = 32000,
    shell:
        """
        mkdir -p {params.output_dir}
        parallel -j {threads} \
            Rscript scripts/FUSION.assoc_test.R \
                --sumstats {input.sumstats} \
                --weights {input.weights} \
                --weights_dir {params.weights_dir} \
                --ref_ld_chr {params.ref_ld_prefix} \
                --chr {{}} \
                --coloc_P {params.coloc_p} \
                --GWASN {params.gwas_n} \
                --out {params.output_dir}/{wildcards.trait}.{{}}.tsv \
            ::: {params.chroms}
        """

rule assemble_assoc_tests:
    input:
        expand('intermediate/{{tissue}}/{{modality}}/{{trait}}/{{trait}}.{chrom}.tsv', chrom=chroms)
    output:
        out = 'output/{tissue}/{modality}/fusion.{tissue}.{modality}.{trait}.tsv'
    params:
        output_dir = 'output/{tissue}/{modality}',
    group: 'twas'
    shell:
        """
        mkdir -p {params.output_dir}
        head -n1 {input[0]} | cut -f3- > {output.out}
        for fname in {input}; do
            tail -n+2 $fname | cut -f3- >> {output.out}
        done
        """

rule assemble_hits_all_traits_modalities:
    input:
        fusion = expand('output/{{tissue}}/{{modality}}/fusion.{{tissue}}.{{modality}}.{trait}.tsv', trait=traits)
    output:
        out = 'output/twas_hits.{tissue}.{modality}.tsv'
    params:
        traits = ' '.join(traits),
    shell:
        """
        head -n1 {input.fusion[0]} \
            | sed 's/^/TRAIT\t/' \
            > {output.out}
        for trait in {params.traits}; do
            tail -n+2 output/{wildcards.tissue}/{wildcards.modality}/fusion.{wildcards.tissue}.{wildcards.modality}.$trait.tsv \
                | awk '$18 < 5e-8' \
                | sed "s/^/$trait\t/" \
                >> {output.out}
        done
        """
